# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=bloodyad
_pyver=3.14
pkgver=2.5.4
pkgrel=1
pkgdesc='An Active Directory Privilege Escalation Framework.'
arch=('any')
groups=('blackarch' 'blackarch-exploitation' 'blackarch-windows')
url='https://github.com/CravateRouge/bloodyAD'
license=('GPL3')
depends=('impacket')
makedepends=('git')
source=("git+https://github.com/CravateRouge/$pkgname.git#tag=v${pkgver}")
sha512sums=('51bd398f63f174526a58678b9803e2f0dde8bdc6aa2f72582c8d0449ef1d50d7490a1d30594153226422ad0ea18b68c144bcfc43ecd9ee3c9d3eb9a45cb85916')

build() {
    cd $pkgname

    python${_pyver} -m venv venv
    source venv/bin/activate
    pip${_pyver} install pyinstaller
    pip${_pyver} install .

    pyinstaller --hidden-import unicrypto.backends.cryptography \
                --hidden-import bloodyAD.cli_modules.add \
                --hidden-import bloodyAD.cli_modules.get \
                --hidden-import bloodyAD.cli_modules.remove \
                --hidden-import bloodyAD.cli_modules.set \
                --onefile bloodyAD.py

    deactivate
}

package() {
    cd $pkgname

    install -Dm 755 ./dist/bloodyAD "$pkgdir/usr/bin/$pkgname"
}
