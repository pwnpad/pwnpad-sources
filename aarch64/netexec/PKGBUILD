# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netexec
_pyver=3.13
pkgver=v1.5.1
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation'
        'blackarch-networking' 'blackarch-windows')
pkgdesc='A Windows / Active Directory environments pentest tool.'
arch=('any')
url='https://netexec.wiki/'
license=('BSD')
# need rust for aardwolf
makedepends=('git' 'rust' 'python-virtualenv')
source=("$pkgname::git+https://github.com/Pennyw0rth/NetExec.git#tag=$pkgver")
sha512sums=('5e7658e4f1b1422cc2f119fe7980782d3e86a6dcee6e0eff7b81e93334fec5b636637ed869458c4303ebb6c864be089bb8ee1b21132356bd8e20962be1706d55')

build() {
  cd $pkgname

  python${_pyver} -m venv venv
  source venv/bin/activate
  pip${_pyver} install pyinstaller pillow
  pip${_pyver} install .

  pyinstaller netexec.spec

  deactivate
}

package() {
  cd $pkgname

  install -Dm 755 ./dist/nxc "$pkgdir/usr/bin/$pkgname"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  ln -s "/usr/bin/$pkgname" "$pkgdir/usr/bin/nxc"

  chmod +x "$pkgdir/usr/bin/$pkgname"
}
