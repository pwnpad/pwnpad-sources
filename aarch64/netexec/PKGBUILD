# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=netexec
_pyver=3.13
pkgver=v1.5.0
pkgrel=1
groups=('blackarch' 'blackarch-scanner' 'blackarch-exploitation'
        'blackarch-networking' 'blackarch-windows')
pkgdesc='A Windows / Active Directory environments pentest tool.'
arch=('any')
url='https://netexec.wiki/'
license=('BSD')
# need rust for aardwolf
makedepends=('git' 'rust' 'python-virtualenv')
source=("$pkgname::git+https://github.com/Pennyw0rth/NetExec.git#tag=$pkgver")
sha512sums=('3ec182c0dfe90988dd78a73b40535b01288a54a389c1ab467159dec785e56376937f4fca25b949d61e1df7c5eb8b2d5db4df95545399d8b5e08bd8b2a2444169')

build() {
  cd $pkgname

  virtualenv venv
  source venv/bin/activate
  pip install pyinstaller . # it does not work with system pyinstaller

  pyinstaller netexec.spec

  deactivate
}

package() {
  cd $pkgname

  install -Dm 755 ./dist/nxc "$pkgdir/usr/bin/$pkgname"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  ln -s "/usr/bin/$pkgname" "$pkgdir/usr/bin/nxc"

  chmod +x "$pkgdir/usr/bin/$pkgname"
}
