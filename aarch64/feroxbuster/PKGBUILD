# This file is part of BlackArch Linux ( https://www.blackarch.org/ ).
# See COPYING for license details.

pkgname=feroxbuster
pkgver=v2.13.1
pkgrel=1
pkgdesc='A fast, simple, recursive content discovery tool written in Rust.'
arch=('x86_64' 'aarch64')
groups=('blackarch' 'blackarch-webapp' 'blackarch-scanner' 'blackarch-fuzzer')
url='https://github.com/epi052/feroxbuster'
license=('MIT')
depends=('openssl' 'pkgconf')
makedepends=('git' 'cargo')
source=("git+https://github.com/epi052/$pkgname.git#tag=${pkgver}")
sha512sums=('804f9b31a0c83a00bd7be05cec3f2e19f007f61f706d5e2a65035d7e224a5c8517ae2d81467993cbc16354fd52d2c745f52774568a98e82dae5e995c46296a8c')

build() {
  cd $pkgname

  # disable lto causing build failing
  # https://archlinux.org/todo/lto-fat-objects/
  # https://github.com/epi052/feroxbuster/blob/b44c52f0ea1fb5a3ac897ccd349e840c9d3440e9/Cargo.toml#L78
  CFLAGS+=" -ffat-lto-objects"

  cargo build --release --locked
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cp -a img/ "$pkgdir/usr/share/doc/$pkgname/"
  cp -a shell_completions/ ferox-config.toml.example \
    "$pkgdir/usr/share/$pkgname/"
}
